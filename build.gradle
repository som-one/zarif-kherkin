plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
}
group 'one.som'
version '1.3.0-RELEASE'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1"
    implementation 'io.github.microutils:kotlin-logging:1.6.24'
    implementation "org.apache.commons:commons-csv:1.5"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.9.7"
    
    testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.3.2'
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

if (project.hasProperty("signing.keyId")) {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                customizePom(pom)
                groupId 'one.som'
                artifactId 'zarif-kherkin'
                version project.version

                pom.withXml {
                    def pomFile = file("${project.buildDir}/generated-pom.xml")
                    writeTo(pomFile)
                    def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                    artifact(pomAscFile) {
                        classifier = null
                        extension = 'pom.asc'
                    }
                }

                from components.java
                artifact(sourceJar) {
                    classifier = 'sources'
                }
                artifact(javadocJar) {
                    classifier = 'javadoc'
                }

                project.tasks.signArchives.signatureFiles.each {
                    artifact(it) {
                        def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                        if (matcher.find()) {
                            classifier = matcher.group(1)
                        } else {
                            classifier = null
                        }
                        extension = 'jar.asc'
                    }
                }
            }
        }
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
    }

    model {
        tasks.generatePomFileForMavenJavaPublication {
            destination = file("$buildDir/generated-pom.xml")
        }
        tasks.publishMavenJavaPublicationToMavenLocal {
            dependsOn project.tasks.signArchives
        }
        tasks.publishMavenJavaPublicationToMavenRepository {
            dependsOn project.tasks.signArchives
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Kherkin DSL framework for creating gherkin-like feature files with more customizability and flexibility.'
            name 'Kherkin'
            url 'https://github.com/som-one/zarif-kherkin/'
            organization {
                name 'one.som'
                url 'http://som.one'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/som-one/zarif-kherkin/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/som-one/zarif-kherkin/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/som-one/zarif-kherkin/'
                connection 'scm:git:git://github.com/som-one/zarif-kherkin.git'
            }
            developers {
                developer {
                    name 'Muhatashim'
                }
            }
        }
    }
}
